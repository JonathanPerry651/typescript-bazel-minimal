load("@rules_java//java:defs.bzl", "java_binary")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index", "oci_tarball")

java_binary(
    name = "server",
    srcs = ["CalculatorServer.java"],
    main_class = "services.calculator.CalculatorServer",
    deps = [
        "//services/calculator/proto:calculator_java_proto",
        "//services/calculator/proto:calculator_java_grpc",
        "@maven//:io_grpc_grpc_api",
        "@maven//:io_grpc_grpc_stub",
    ],
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "layer",
    srcs = [":server_deploy.jar"],
    package_dir = "/app",
)

oci_image(
    name = "image",
    base = "@distroless_java",
    entrypoint = ["java", "-jar", "/app/server_deploy.jar"],
    tars = [":layer"],
    annotations = {
        "org.opencontainers.image.ref.name": "localhost/calculator-server:latest",
    },
)

oci_image_index(
    name = "image_index",
    images = [":image"],
)

genrule(
    name = "tarball",
    srcs = [":image"],
    outs = ["calculator.tar"],
    cmd = """
        mkdir -p image_mod
        cp -RL $(location :image)/* image_mod/
        chmod -R u+w image_mod
        python3 -c "import json; f='image_mod/index.json'; d=json.load(open(f)); d['manifests'][0]['annotations']={'org.opencontainers.image.ref.name':'localhost/calculator-server:latest'}; json.dump(d,open(f,'w'))"
        tar -C image_mod -cf $@ .
    """,
    visibility = ["//visibility:public"],
)
